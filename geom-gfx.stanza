;; See license.txt for details about licensing.

defpackage geom/gfx:
  import core
  import math
  import collections
  import geom/vec
  import geom/mat
  import geom/box
  import geom/polyline
  import geom/polygon
  import geom/mesh
  import geom/triangle
  import geom/line-segment
  import geom/pixels
  import geom/bounded
  import geom/poseable
  import geom/assembly
  import geom/geom
  import geom/arc-ball
  import gfx/gl
  import gfx/drawing
  import gfx/font

extern glLoadMatrixf: (ptr<float>) -> int
extern glMultMatrixf: (ptr<float>) -> int

public defn gl-color3 (v:V3f) : gl-color3f(x(v), y(v), z(v))

public defn gl-color4 (v:V4f) : gl-color4f(x(v), y(v), z(v), a(v))

public defn gl-vertex3 (v:V3f) : gl-vertex3f(x(v), y(v), z(v))

public defn gl-vertex2 (v:V2f) : gl-vertex2f(x(v), y(v))

public defn gl-translate3 (v:V3f) : gl-translatef(x(v), y(v), z(v))

public defn gl-translate2 (v:V2f) : gl-translatef(x(v), y(v), 0.0f)

public defmulti render (g:Geom) -> False

public defn compile (g:Geom) -> DisplayList :
  val dl = gl-gen-list()
  gl-new-list(dl, GL-COMPILE)
  render(g)
  gl-end-list(dl)
  dl

defmethod render (p:V3f) -> False :
  gl-begin(GL-POINTS)
  gl-vertex3(p)
  gl-end()

defmethod render (p:V2f) -> False :
  gl-begin(GL-POINTS)
  gl-vertex2(p)
  gl-end()

defmethod render (box:Box3f) -> False :
  gl-begin(GL-LINE-LOOP)
  gl-vertex3(lo(box))
  gl-vertex3(hi(box))
  gl-end()

defmethod render (msg:Msg) -> False :
  draw-text(2.0f, 2.0f, *font*, value(msg))

defmethod render (poly:PolyLine3f) -> False :
  gl-push-attrib(GL-CURRENT-BIT)
  gl-color(color(poly))
  for s in strokes(poly) do :
    gl-begin(GL-LINE-STRIP)
    for p in s do :
      gl-vertex3(p)
    gl-end()
  gl-pop-attrib()

defmethod render (poly:PolyLine2f) -> False :
  gl-push-attrib(GL-CURRENT-BIT)
  gl-color(color(poly))
  for s in strokes(poly) do :
    gl-begin(GL-LINE-STRIP)
    for p in s do :
      gl-vertex2(p)
    gl-end()
  gl-pop-attrib()

defmethod render (seg:LineSegment2f) -> False :
  gl-begin(GL-LINES)
  gl-vertex2(start(seg))
  gl-vertex2(end(seg))
  gl-end()

defmethod render (seg:LineSegment3f) -> False :
  gl-begin(GL-LINES)
  gl-vertex3(start(seg))
  gl-vertex3(end(seg))
  gl-end()

defmethod render (tri:Triangle) -> False :
  gl-begin(GL-LINE-LOOP)
  for p in vertices(tri) do :
    gl-vertex2(p)
  gl-end()

defmethod render (contour:Contour) -> False :
  gl-begin(GL-LINE-LOOP)
  for p in vertices(contour) do :
    gl-vertex2(p)
  gl-end()

defmethod render (poly:Polygon) -> False :
  gl-push-attrib(GL-CURRENT-BIT)
  gl-color(color(poly))
  for c in contours(poly) do :
    render(c)
  gl-pop-attrib()

defmethod render (a:Assembly) -> False :
  gl-push-attrib(GL-CURRENT-BIT)
  gl-color(color(a))
  gl-push-matrix()
  gl-mult-matrix(mat(a))
  for c in children(a) do :
    render(c)
  gl-pop-matrix()
  gl-pop-attrib()

defmethod render (mesh:Mesh) -> False :
  val verts = vertices(mesh)
  gl-push-attrib(GL-LINE-BIT | GL-CURRENT-BIT)
  gl-color(color(mesh))
  for t in faces(mesh) do :
    gl-begin(GL-TRIANGLES)
    gl-vertex3(verts[x(t)])
    gl-vertex3(verts[y(t)])
    gl-vertex3(verts[z(t)])
    gl-end()
  gl-line-width(2.0f)
  gl-color4f(0.5f, 0.2f, 0.2f, 0.4f)
  for t in faces(mesh) do :
    gl-begin(GL-LINE-LOOP)
    gl-vertex3(verts[x(t)])
    gl-vertex3(verts[y(t)])
    gl-vertex3(verts[z(t)])
    gl-end()
  gl-pop-attrib()

defmethod render (cells:Pixels) :
  gl-push-attrib(GL-LINE-BIT | GL-CURRENT-BIT)
  val dim = dim(cells)
  gl-push-matrix()
  val dims = idims(cells)
  val ext = 0.5f * dim * to-v3f(dims)
  gl-push-matrix()
  val off = -1.0f * ext + xyz(V2f-fill(0.5f * dim))
  gl-translatef(x(off), y(off), 0.0f)
  for pos in indices(dims) do :
    gl-push-matrix()
    val p = to-v3f(pos)
    val c = cells[pos]
    gl-translatef(x(p) * dim, y(p) * dim, 0.0f)
    gl-scalef(0.95f, 0.95f, 0.95f)
    gl-color4f(to-float $ c, 0.0f, 0.0f, 0.8f)
    draw-quad(dim, dim)
    gl-pop-matrix()
  gl-pop-matrix()
  gl-color4f(1.0f, 1.0f, 1.0f, 1.0f)
  draw-rect(to-float(x(dims)) * dim, to-float(y(dims)) * dim)
  gl-pop-matrix()
  gl-pop-attrib()

public lostanza defn gl-load-matrix (m:ref<Mat44f>) -> ref<False> :
  call-c glLoadMatrixf(addr!(m.m00))
  return false

public lostanza defn gl-mult-matrix (m:ref<Mat44f>) -> ref<False> :
  call-c glMultMatrixf(addr!(m.m00))
  return false

public defn render-begin (t:Transformation) :
  gl-matrix-mode(GL-PROJECTION)
  gl-push-matrix()
  gl-translate2(mov(t))
  gl-scalef(mag(t), mag(t), mag(t))
  gl-matrix-mode(GL-MODELVIEW)
  gl-push-matrix()
  gl-mult-matrix(rot(ball(t)))

public defn render-model-begin (t:Transformation) :
  gl-matrix-mode(GL-PROJECTION)
  gl-push-matrix()
  gl-matrix-mode(GL-MODELVIEW)
  gl-push-matrix()
  ;; gl-translate2(mov(t))
  ;; gl-scalef(mag(t), mag(t), mag(t))
  val imov = -1.0f * mov(t) / mag(t)
  val imag = 1.0f / mag(t)
  gl-translate2(imov)
  gl-scalef(imag, imag, imag)
  gl-mult-matrix(rot(ball(t)))

public defn render-end (t:Transformation) :
  gl-pop-matrix()
  gl-matrix-mode(GL-PROJECTION)
  gl-pop-matrix()
  gl-matrix-mode(GL-MODELVIEW)

